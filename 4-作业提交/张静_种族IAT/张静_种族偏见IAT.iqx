//1-定义呈现内容
**************************************************************************************************************
**************************************************************************************************************
//定义刺激标签
<item attributeAlabel>
	/1 = "好"
</item>

<item attributeBlabel>
	/1 = "坏"
</item>

<item targetALabel>
	/1 = "白人"
</item>

<item targetBLabel>
	/1 = "黑人"
</item>

// 定义概念词
<item attributeA>
	/1 = "有趣"
	/2 = "极佳"
	/3 = "豪华"
	/4 = "完美"
</item>

<item attributeB>
	/1 = "可怕"
	/2 = "糟糕"
	/3 = "差劲"
	/4 = "讨厌"
</item>

//定义刺激图片
<item targetA>
	/1 = "white1.jpg"
	/2 = "white2.jpg"
	/3 = "white3.jpg"
	/4 = "white4.jpg"
</item>

<item targetB>
	/1 = "black1.jpg"
	/2 = "black2.jpg"
	/3 = "black3.jpg"
	/4 = "black4.jpg"
</item>


//定义用到的指导语，用instruct标记符
<instruct start>
/nextkey = (" ")
/ fontstyle = ("微软雅黑 Light", 3.26%, false, false, false, false, 5, 1)
/ txcolor = (255, 99, 71)
/screencolor = (255,255,255)
/lastlabel = "按空格键继续"
/windowsize = (100%,100%)
</instruct>

<page instruction>
^^
在这个任务中，需要您根据呈现的词汇或图片做出按键反应^^
如果呈现的是'<%item.attributeALabel.item.1%>' 的词汇或者 <%item.targetALabel.1%> 或 <%item.targetBLabel.1%> 的图片，请按 ~"I~" 键^^
如果呈现的是'<%item.attributeBLabel.item.1%>' 的词汇或者不同于 <%item.targetALabel.1%> 和 <%item.targetBLabel.1%> 的图片，请按~"E~" 键^^
最开始的几组练习会帮您熟悉这种按键设定，请尽快地按键反应并尽量少地犯错^^
如果您按错了键，屏幕上会呈现一个红色的 ~"X~"^^
这样的情况下请尽快地按另外一个键来更正您的反应^^
</page>


// 定义每个block的指导语
<item testinstructions>
	/ 1 = "看到 <%item.attributeALabel.item.1%> 类的词汇或 <%values.currentTarget%> 的图片，请按 I 键~n~n看到其他类别的词汇或图片请按 E 键~n~n~n您的反应越快越好~n~n请按空格键开始"
</item>

// 指导语界面上呈现给被试“好”类的词汇
<item attributeAFocusInstruct>
	/ 1 = "<%item.attributeALabel.1%>~n<%item.attributeA.1%> <%item.attributeA.2%> <%item.attributeA.3%> <%item.attributeA.4%>"
</item>

// 指导语界面上呈现给被试的白人的图片
<picture targetAinstruct1>
    /items=("white1.jpg")
    /position = (29,20)
	/size=(10%,16%)
    /valign = center
    / halign = center
</picture>

<picture targetAinstruct2>
    /items=("white2.jpg")
    /position = (43,20)
	/size=(10%,16%)
    /valign = center
    / halign = center
</picture>

<picture targetAinstruct3>
    /items=("white3.jpg")
    /position = (57,20)
	/size=(10%,16%)
    /valign = center
    / halign = center
</picture>

<picture targetAinstruct4>
    /items=("white4.jpg")
    /position = (71,20)
	/size=(10%,16%)
    /valign = center
    / halign = center
</picture>

// 指导语界面上呈现给被试的黑人的图片
<picture targetBinstruct1>
    /items=("black1.jpg")
    /position = (29,20)
	/size=(10%,16%)
    /valign = center
    / halign = center
</picture>

<picture targetBinstruct2>
    /items=("black2.jpg")
    /position = (43,20)
	/size=(10%,16%)
    /valign = center
    / halign = center
</picture>

<picture targetBinstruct3>
    /items=("black3.jpg")
    /position = (57,20)
	/size=(10%,16%)
    /valign = center
    / halign = center
</picture>

<picture targetBinstruct4>
    /items=("black4.jpg")
    /position = (71,20)
	/size=(10%,16%)
    /valign = center
    / halign = center
</picture>

// 指导语界面上呈现给被试的白人黑人的标签
<item targetAFocusInstruct>
	/ 1 = "<%item.targetALabel.1%>"
</item>

<item targetBFocusInstruct>
	/ 1 = "<%item.targetBLabel.1%>"
</item>



// 2-定义指导语及刺激的呈现方式
// 老师示例的界面非常漂亮，我就照搬了，只微微调整了一下位置好放图片~
**************************************************************************************************************
**************************************************************************************************************
<text testInstructions>
/ items = testInstructions
/ size = (90%, 50%)
/ position = (50%, 50%)
/ valign = top
/ hjustify = center
/ vjustify = top
</text>

//items在block中得到定义，是白人类别还是黑人类别
<text topFocusInstruct>
/ items = ("")
/ valign = bottom
/ halign = center
/ position = (50%, 15%)
/ size = (100%, 12%)
/ txcolor = aqua
/ fontstyle = ("Arial", 5%)
</text>

<text orInstruct>
/ items = ("or")
/ valign = center
/ halign = center
/ position = (50%, 32.5%)
/ txcolor = white
/ fontstyle = ("Arial", 3%)
</text>

//“好”类词的呈现方式
<text bottomFocusInstruct>
/ items = attributeAFocusInstruct
/ valign = top
/ halign = center
/ position = (50%, 35%)
/ size = (100%, 12%)
/ txcolor = yellow
/ fontstyle = ("Arial", 5%)
</text>


// 定义刺激词汇的呈现方式，白人和黑人用黄色显示，花和虫用水绿色显示，用于提醒被试呈现刺激的类别
<text attributeA>
/ items = attributeA
/ fontstyle = ("Arial", 10%)
/ txcolor = yellow
</text>

<text attributeB>
/ items = attributeB
/ fontstyle = ("Arial", 10%)
/ txcolor = yellow
</text>

// 定义刺激图片的呈现方式
<picture targetA>
    /items=targetA
    /position = (50,50)
	/size=(300,400)
    /valign = center
    / halign = center
</picture>

<picture targetB>
    /items=targetB
    /position = (50,50)
	/size=(300,400)
    /valign = center
    / halign = center
</picture>

// 定义错误反应
<text error>
/ position = (50%, 85%)
/ items = ("×")
/ color = red
/ fontstyle = ("Arial", 10%)
</text>

// 指导语页面各部分呈现方式
<text attributeAFocusLabel>
/ items = attributeALabel
/ valign = top
/ halign = center
/ position = (50%, 5%)
/ txcolor = yellow
/ fontstyle = ("Arial", 4%)
/ erase = false
</text>

<text targetFocusLabel>
/ items = ("<%values.currentTarget%>")
/ valign = top
/ halign = center
/ position = (50%, 5%)
/ txcolor = aqua
/ fontstyle = ("Arial", 4%)
/ erase = false
</text>

<text orLabel>
/ items = ("or")
/ valign = top
/ halign = center
/ position = (50%, 10%)
/ txcolor = white
/ fontstyle = ("Arial", 3%)
/ erase = false
</text>


<text orAttributeAFocusLabel>
/ items = attributeALabel
/ valign = top
/ halign = center
/ position = (50%, 15%)
/ txcolor = yellow
/ fontstyle = ("Arial", 4%)
/ erase = false
</text>

// 定义实验进程，进行到哪个block了
<text progress>
/ position = (50%, 95%)
/ items = ("(Block <%expressions.currentblocknumber%> of <%expressions.totalblockcount %>)")
/ color = white
/ fontstyle = ("Arial", 2%)
</text>

//3-定义trial
**************************************************************************************************************
**************************************************************************************************************
// 定义类别为“好”的试次
<trial attributeA>
/ validresponse = ("E", "I")
/ correctresponse = ("I")
/ stimulusframes = [1 = attributeA]
/ posttrialpause = 250
</trial>

// 定义类别为“坏”的试次
<trial attributeB>
/ validresponse = ("E", "I")
/ correctresponse = ("E")
/ stimulusframes = [1 = attributeB]
/ posttrialpause = 250
</trial>

// 定义类别为“白人”、正确按键为左边的“E”的试次
<trial targetALeft>
/ validresponse = ("E", "I")
/ correctresponse = ("E")
/ stimulusframes = [1 = targetA]
/ posttrialpause = 250
</trial>

// 定义类别为“白人”、正确按键为右边的“I”的试次
<trial targetARight>
/ validresponse = ("E", "I")
/ correctresponse = ("I")
/ stimulusframes = [1 = targetA]
/ posttrialpause = 250
</trial>

// 定义类别为“黑人”、正确按键为左边的“E”的试次
<trial targetBLeft>
/ validresponse = ("E", "I")
/ correctresponse = ("E")
/ stimulusframes = [1 = targetB]
/ posttrialpause = 250
</trial>

// 定义类别为“黑人”、正确按键为右边的“I”的试次
<trial targetBRight>
/ validresponse = ("E", "I")
/ correctresponse = ("I")
/ stimulusframes = [1 = targetB]
/ posttrialpause = 250
</trial>

// 定义“白人”和“好”同一按键的block的指导语界面
<trial testinstructionsA>
/ stimulustimes = [1=testinstructions, topFocusInstruct, orInstruct, bottomFocusInstruct, progress,targetAinstruct1, targetAinstruct2, targetAinstruct3,targetAinstruct4]
/ correctresponse = (" ")
/ errormessage = false
/ recorddata = false
</trial>

// 定义“黑人”和“好”同一按键的block的指导语界面
<trial testinstructionsB>
/ stimulustimes = [1=testinstructions, topFocusInstruct, orInstruct, bottomFocusInstruct, progress,targetBinstruct1, targetBinstruct2, targetBinstruct3,targetBinstruct4]
/ correctresponse = (" ")
/ errormessage = false
/ recorddata = false
</trial>

<trial showAttributeLabel>
/ stimulusframes = [1=attributeAFocusLabel]
/ response = noresponse
/ recorddata = false
</trial>


<trial showLabels>
/ stimulusframes = [1=targetFocusLabel, orLabel, orAttributeAFocusLabel]
/ response = noresponse
/ recorddata = false
</trial>

//定义反馈信息界面
<trial summary>
/ ontrialbegin = [values.magnitude = "little to no"]
/ ontrialbegin = [if( abs(expressions.d) > 0.15 ) values.magnitude = "a slight"]
/ ontrialbegin = [if( abs(expressions.d) > 0.35 ) values.magnitude = "a moderate"]
/ ontrialbegin = [if( abs(expressions.d) >= 0.65 ) values.magnitude = "a strong"]
/ ontrialbegin = [if (expressions.d >= 0.0) expressions.preferred = item.targetALabel.1]
/ ontrialbegin = [if (expressions.d < 0.0) expressions.preferred = item.targetBLabel.1]
/ ontrialbegin = [if (expressions.d < 0.0) expressions.notpreferred= item.targetALabel.1]
/ ontrialbegin = [if (expressions.d >= 0.0) expressions.notpreferred= item.targetBLabel.1]
/ stimulustimes = [0=summary]
/ validresponse = (" ")
/ recorddata = false
/ skip = [values.skipsummary]
</trial>

//定义反馈信息
<text summary>
/ items = ("Your IAT score (D) was <% expressions.d %>, which suggests <% values.magnitude %> automatic preference for <% expressions.preferred %> compared to <% expressions.notpreferred %>.~n~n~nPress the spacebar to complete this session.")  
/ size = (60%, 80%)
/ hjustify = left
 </text>

// 4- 定义block和expt
**************************************************************************************************************
**************************************************************************************************************
//定义练习阶段的“白人”和“好”同一按键的block
<block short_a>
/ onblockbegin = [values.currentTarget = item.targetALabel.1; text.topFocusInstruct.1 = item.targetAFocusInstruct.1]
/ trials = [1=testInstructionsA; 2=showLabels;
  3-6 = noreplace(targetARight, targetBLeft);
  8,10,12,14 = noreplace(targetARight, targetBLeft);
  7,9,11,13 = noreplace(attributeA, attributeB)]
/ errormessage = true(error,200)
/ responsemode = correct
</block>

//定义练习阶段的“黑人”和“好”同一按键的block
<block short_b>
/ onblockbegin = [values.currentTarget = item.targetBLabel.1; text.topFocusInstruct.1 = item.targetBFocusInstruct.1]
/ trials = [1=testInstructionsB; 2=showLabels;
  3-6 = noreplace(targetBRight, targetALeft);
  8,10,12,14 = noreplace(targetBRight, targetALeft);
  7,9,11,13 = noreplace(attributeA, attributeB)]
/ errormessage = true(error,200)
/ responsemode = correct
</block>

//定义正式实验阶段的“白人”和“好”同一按键的block
<block A>
/ skip = [values.extended == false && block.A.totalcount >=1]
/ onblockbegin = [values.currentTarget = item.targetALabel.1; text.topFocusInstruct.1 = item.targetAFocusInstruct.1]
/ trials = [1=testInstructionsA; 2=showLabels;
  3-6 = noreplace(targetARight, targetBLeft);
  8,10,12,14,16,18,20,22 = noreplace(targetARight, targetBLeft);
  7,9,11,13,15,17,19,21 = noreplace(attributeA, attributeB)]
/ errormessage = true(error,200)
/ responsemode = correct
/ ontrialend = [if (block.A.totalcount == 0 && block.A.latency  <= 10000 && block.A.currenttrialnumber >= 7 ) values.A1sum =  values.A1sum + block.A.latency]
/ ontrialend = [if (block.A.totalcount == 0 && block.A.latency  <= 10000 && block.A.currenttrialnumber >= 7 ) values.A1n += 1]
/ ontrialend = [if (block.A.totalcount == 0 && block.A.latency  <= 10000 && block.A.currenttrialnumber >= 7 ) values.A1ss =  values.A1ss  + (block.A.latency * block.A.latency)]
/ ontrialend = [if (block.A.totalcount == 1 && block.A.latency  <= 10000 && block.A.currenttrialnumber >= 7 ) values.A2sum =  values.A2sum + block.A.latency]
/ ontrialend = [if (block.A.totalcount == 1 && block.A.latency  <= 10000 && block.A.currenttrialnumber >= 7 ) values.A2n += 1]
/ ontrialend = [if (block.A.totalcount == 1 && block.A.latency  <= 10000 && block.A.currenttrialnumber >= 7 ) values.A2ss =  values.A2ss  + (block.A.latency * block.A.latency)]
/ ontrialend = [if (block.A.latency  <= 10000 && block.A.currenttrialnumber >= 7 ) values.n_correct += block.a.correct]
</block>

//定义正式实验阶段的“黑人”和“好”同一按键的block
<block B>
/ skip = [values.extended == false && block.B.totalcount >=1]
/ onblockbegin = [values.currentTarget = item.targetBLabel.1; text.topFocusInstruct.1 = item.targetBFocusInstruct.1]
/ trials = [1=testInstructionsB;2=showLabels;
  3-6 = noreplace(targetBRight, targetALeft);
  8,10,12,14,16,18,20,22= noreplace(targetBRight, targetALeft);
  7,9,11,13,15,17,19,21= noreplace(attributeA, attributeB)]
/ errormessage = true(error,200)
/ responsemode = correct
/ ontrialend = [if (block.B.totalcount == 0 && block.B.latency  <= 10000 && block.B.currenttrialnumber >= 7 ) values.B1sum =  values.B1sum + block.B.latency]
/ ontrialend = [if (block.B.totalcount == 0 && block.B.latency  <= 10000 && block.B.currenttrialnumber >= 7 ) values.B1n +=  1]
/ ontrialend = [if (block.B.totalcount == 0 && block.B.latency  <= 10000 && block.B.currenttrialnumber >= 7 ) values.B1ss =  values.B1ss  + (block.B.latency * block.B.latency)]
/ ontrialend = [if (block.B.totalcount == 1 && block.B.latency  <= 10000 && block.B.currenttrialnumber >= 7 ) values.B2sum =  values.B2sum + block.B.latency]
/ ontrialend = [if (block.B.totalcount == 1 && block.B.latency  <= 10000 && block.B.currenttrialnumber >= 7 ) values.B2n +=  1]
/ ontrialend = [if (block.B.totalcount == 1 && block.B.latency  <= 10000 && block.B.currenttrialnumber >= 7 ) values.B2ss =  values.B2ss  + (block.B.latency * block.B.latency)]
/ ontrialend = [if (block.B.latency  <= 10000 && block.B.currenttrialnumber >= 7 ) values.n_correct += block.B.correct]
</block>

//定义反馈的block
<block summary>
/ trials = [1=summary]
/ recorddata = false
</block>



<expt>
/ subjects = (1 of 2)
/ groupassignment = groupnumber
/preinstructions = (instruction)
/ blocks = [1 = short_a; 2 = short_b; 3=A; 4=B; 5=A; 6=B; 7=summary]
/onexptend = [values.completed = 1]
</expt>

<expt>
/ subjects = (2 of 2)
/ groupassignment = groupnumber
/preinstructions = (instruction)
/ blocks = [1 = short_b; 2 = short_a; 3=B; 4=A; 5=B; 6=A; 7=summary]
/onexptend = [values.completed = 1]
</expt>


//5-定义data file和用到的变量及表达式
***********************************************************************
***********************************************************************
<data>
/file = "BriefIAT_rawdata.iqdat"
/ columns = [date, time, group, subject, blockcode, blocknum, trialcode, trialnum, response, correct, latency, 
					stimulusnumber, stimulusitem, expressions.d1, expressions.d2, expressions.d, expressions.percentcorrect]
/separatefiles = true
</data>

<summarydata >
/file = "BriefIAT_summary.iqdat"
/columns = [script.filename, script.startdate, script.starttime, script.subjectid, script.groupid, script.elapsedtime, values.completed,
expressions.d1, expressions.d2, expressions.d, expressions.percentcorrect]
</summarydata>

//定义变量及初始值
<values>
/completed = 0
/ A1sum = 0
/ A2sum = 0
/ B1sum = 0
/ B2sum = 0
/ A1n = 0
/ A2n = 0
/ B1n = 0
/ B2n = 0
/ A1ss = 0
/ A2ss = 0
/ B1ss = 0
/ B2ss = 0
/ magnitude = "unknown"
/ currentTarget = ""
/ n_correct = 0
</values>


//定义表达式的计算
<expressions>
/ A1m = values.A1sum / values.A1n
/ A2m = values.A2sum / values.A2n
/ B1m = values.B1sum / values.B1n
/ B2m = values.B2sum / values.B2n
/ A1sd = sqrt((values.A1ss - (values.A1n* (expressions.A1m * expressions.A1m))) / (values.A1n - 1))
/ A2sd = sqrt((values.A2ss - (values.A2n* (expressions.A2m * expressions.A2m))) / (values.A2n - 1))
/ B1sd = sqrt((values.B1ss - (values.B1n* (expressions.B1m * expressions.B1m))) / (values.B1n - 1))
/ B2sd = sqrt((values.B2ss - (values.B2n* (expressions.B2m * expressions.B2m))) / (values.B2n - 1))
/ sd1 = sqrt((((values.A1n - 1) * (expressions.A1sd * expressions.A1sd) + (values.B1n - 1) * (expressions.B1sd * expressions.B1sd)) + ((values.A1n + values.B1n) * ((expressions.A1m - expressions.B1m) * (expressions.A1m - expressions.B1m)) / 4) ) / (values.A1n + values.B1n - 1) )
/ sd2 = sqrt((((values.A2n - 1) * (expressions.A2sd * expressions.A2sd) + (values.B2n - 1) * (expressions.B2sd * expressions.B2sd)) + ((values.A2n + values.B2n) * ((expressions.A2m - expressions.B2m) * (expressions.A2m - expressions.B2m)) / 4) ) / (values.A2n + values.B2n - 1) )
/ d1 = (expressions.B1m - expressions.A1m) / expressions.sd1
/ d2 = (expressions.B2m - expressions.A2m) / expressions.sd2
/ d = if ( values.extended ) { (d1+d2) / 2 } else { d1 }
/ currentblocknumber = max(expt.1.currentblocknumber, expt.2.currentblocknumber)
/ preferred = "unknown"
/ notpreferred = "unknown"
/ totalblockcount = if (values.extended) {expt.1.blockcount} else {expt.1.blockcount-2}
/ percentcorrect = (values.n_correct/ (values.a1n + values.a2n + values.b1n + values.b2n)) * 100
</expressions>


//定义默认参数（字体、背景颜色、屏幕颜色、实验程序运行的最低版本）的属性值
<defaults>
/ fontstyle = ("Arial", 3.5%)
/ screencolor = black
/ txbgcolor = black
/ txcolor = white
/ minimumversion = "4.0.0.0"
</defaults>


<values>
/ skipsummary = false
/ extended = true
</values>

